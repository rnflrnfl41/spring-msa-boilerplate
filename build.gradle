plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

ext {
    springBootVersion = '3.3.5'
    springCloudVersion = '2023.0.3'
}

//모든 플젝이 공유할 기본 설정
allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    description = 'springMsaBoilerplate'

    repositories {
        mavenCentral()
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    //Spring Cloud 라이브러리 버전 관리
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    //모든 프로젝트 공통 테스트 라이브러리
    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // Logging
        implementation 'org.slf4j:slf4j-api:2.0.9'

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
    }

    //테스트 실행 방식 설정
    tasks.named('test') {
        useJUnitPlatform()
    }
}
